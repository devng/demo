apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
	options.compilerArgs << '-Xlint:all'
}

compileTestJava {
	options.compilerArgs << '-Xlint:all'
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14.1'
}

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'com.sparkjava', name: 'spark-core', version: '2.5.5'
	compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.13'
	compile group: 'com.owlike', name: 'genson', version: '1.4'

	testCompile group: 'junit', name: 'junit', version: '4.+'
}

// Create a fat executable jar
jar {
	manifest {
		attributes "Main-Class": "com.devng.spark.SparkApp"
	}

	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}

	archiveName "app.jar"
}

task dockerBuild(dependsOn: jar, type: Exec) {
	workingDir "${projectDir}"
	commandLine 'docker'
	args 'build', '-t', 'sparkjava-demo:v1', '.'
}

build.dependsOn dockerBuild